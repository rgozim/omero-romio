task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

publishing {
    publications {
        romioJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

artifactory {
    contextUrl = "http://artifacts.openmicroscopy.org/artifactory"
    publish {
        repository {
            repoKey = 'integration-libs' // The Artifactory repository key to publish to
            username = System.getenv('ARTIFACTORY_USER') ?: "${findProperty('artifactorUser')}"
            password = System.getenv('ARTIFACTORY_PASSWORD') ?: "${findProperty('artifactorPassword')}"
        }
    }
}
